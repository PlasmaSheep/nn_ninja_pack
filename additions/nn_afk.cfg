//======================================================================================
// Ninja Config v2 - AFK Script
//======================================================================================
//
// 2009 by Bommel (aka Spohst/FrogonCoffee...godamnit I need a database for my names.) - You can use it as you want ;) (version 0.9)
//
// Usage: afk      [minutes_you're_probalby_afk]
//        afkr     [minutes_you're_probalby_afk] [reason]
//        afkstop: Stops the script.
//        
//        afkadd [minutes]:  [minutes] + remaining time.
//        afksub [minutes]:  [minutes] - remaining time.
//        afkmul [minutes]:  [minutes] * remaining time.
//        afkdiv [minutes]:  [minutes] / remaining time.
//        afkmod [minutes]:  [minutes] % remaining time. (xD)
//       
//        afkset [minutes]:  Sets the remaning time to [minutes]
//
// AFK SCRIPT --------------------------------------------------------------------------
// This file contains a script that allows you to set time you'll be afk and a reason. -
// -------------------------------------------------------------------------------------

// CONFIGURATION
// Modify these to your liking.
seta afk_message "^0brb^3 "
seta afk_time_unit " ^0min."
seta afk_backmsg "^xD60<-^0back."


// Other vars. Better not to change them.
seta afk_interval 60
seta afk_counter 0       
alias afk_newline "echo; echo ------------------------"
seta afk_cmd_message "^4Your name will be changed back in "



// Commands to change the remaining time:
alias afkadd "menu_cmd rpn /afk_counter afk_counter $1 +    def; afk_upname;  nn_gmessage $afk_counter Minute(s) left."
alias afksub "menu_cmd rpn /afk_counter afk_counter $1 -    def; afk_upname;  nn_gmessage $afk_counter Minute(s) left."
alias afkmul "menu_cmd rpn /afk_counter afk_counter $1 *    def; afk_upname;  nn_gmessage $afk_counter Minute(s) left."
alias afkdiv "menu_cmd rpn /afk_counter afk_counter $1 /    def; afk_upname;  nn_gmessage $afk_counter Minute(s) left."
alias afkmod "menu_cmd rpn /afk_counter afk_counter $1 mod  def; afk_upname;  nn_gmessage $afk_counter Minute(s) left."
alias afkset "seta afk_counter $1; afk_upname"



// 'afk' and 'brb' are just a 'pointers' to 'afkr' with missing second argument.
alias brb "afkr $1 \"\""
alias afk "afkr $1 \"\""

// Setting everything up...
alias afkr "afk_newline; seta old_name \"$_cl_name\"; seta secfoo $1; seta rea \" $2\"; seta afk_counter $1; nn_gmessage ^4... $1 minutes afk...^7; menu_cmd rpn /secfoo secfoo $afk_interval * def; afk_correct"
alias afk_p "nn_gmessage $afk_cmd_message ^1 $secfoo seconds; afk_newline; nn_gmessage; say $afk_message$afk_counter$afk_time_unit; update_loop"
alias afk_fin "defer $afk_interval nafk; echo"
alias nafk  "nn_gmessage ^3Stopping Script!; afkstop"


// If switches - checking if the given number is valid.
alias afk_correct "afkco"
alias afkco  "menu_cmd rpn /result  $afk_counter 0 eq def; afkco2"
alias afkco2 "switch_$result"
alias afkco3  "menu_cmd rpn /result  $afk_counter 1 mod def; afkco4"
alias afkco4 "witch_$result"

alias switch_1 "nn_gmessage ^1afk 0. Hahaha. Verrry funny."
alias switch_0 "afkco3"
alias witch_0 "afk_p"
alias witch_1 "nn_gmessage .x (float) numbers are not allowed."



// 'decrement' = afk_counter--; in common languages... (and that's afk_counter = afk_counter - 1)
alias afk_decrement  "menu_cmd rpn /afk_counter afk_counter 1 - def"
alias afk_upname  "name $old_name$afk_message$afk_counter$afk_time_unit$rea"

// The Main Loop that updates the name & checks if the afk_counter = 1, if so it executes "afk_fin"
alias update_loop  "afk_upname; afk_compare $afk_counter"
alias afk_lp_defer "afk_decrement; defer $afk_interval update_loop"

// This 'if' checks if the script needs to be terminated:
alias afk_compare "menu_cmd rpn /result 1 $1 eq def; afk_compare2"
alias afk_compare2 "afk_answer_$result"
alias afk_answer_1 "afk_fin"
alias afk_answer_0 "afk_lp_defer"


// Stops loop and loads it after a while again.
alias afkstop "alias update_loop \"\"; name $old_name; say $afk_backmsg; defer 10 afk_exacfg"
alias afk_exacfg  "nn_gmessage ^3Reinitializing AFK-script:^7;exec autoexec.cfg" 

// END AFK SCRIPT ----------------------------------------------------------------------
// -------------------------------------------------------------------------------------
